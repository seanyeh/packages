pkgname=(openldap)
pkgver=2.4.31
pkgrel=1
pkgdesc="OpenLDAP Lightweight Directory Access Protocol implementation."
arch=("x86_64")
url="http://www.openldap.org"
license=(custom)

backup=()

source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz)
sha256sums=(bde845840df4794b869a6efd6a6b1086f80989038e4844b2e4d7d6b57b39c5b6)

build() {
	cd "${srcdir}/openldap-${pkgver}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --localstatedir=/var/lib \
				--sysconfdir=/etc --mandir=/usr/man --with-cyrus-sasl \
				--with-tls --with-threads --enable-syslog \
				--enable-dynamic --enable-local --enable-proctitle \
				--disable-static --enable-shared --disable-slapd
	make depend
	make
}

package_openldap() {
	#depends=('libldap>=${pkgver}')
	backup=('etc/openldap/ldap.conf')

	cd "${srcdir}"/openldap-${pkgver}
	make install DESTDIR="${pkgdir}"

	# Fix permissions
	chmod 755 "${pkgdir}"/usr/lib/${MULTIARCH}/*.so.*

	# Install copyright and docs stuff
	mkdir -p "${pkgdir}"/usr/doc/openldap-${pkgver}
	cp -a COPYRIGHT LICENSE README "${pkgdir}"/usr/doc/openldap-${pkgver}/

	# We don't ship the servers right now, so remove manpages
	find "${pkgdir}"/usr/man -name slap* -exec rm -f {} \;
	find "${pkgdir}"/usr/man -name slurp* -exec rm -f {} \;
	#find "${pkgdir}"/usr/man -type d -empty -exec rmdir {} \;
}
