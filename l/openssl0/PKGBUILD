pkgname=openssl0
pkgver=0.9.8zc
pkgrel=1
pkgdesc="A legacy cryptography toolkit."
arch=("x86_64")
url="http://www.openssl.org"
license=("custom")

depends=('glibc')
provides=("openssl=${pkgver}")
backup=('etc/ssl/openssl.cnf')

source=("http://www.openssl.org/source/openssl-${pkgver}.tar.gz"
		openssl.soname.diff.gz)
sha256sums=(461cc694f29e72f59c22e7ea61bf44671a5fc2f8b3fc2eeac89714b7be915881
			922d68e43c06ccd56bd430a81d7ad752f620b5fb1b37e857d9a3662273c11d46)

prepare() {
	cd "${srcdir}/openssl-${pkgver}"
	
	# Lower the internal OpenSSL version number to improve application
	# compatibility. This is very naughty, but it's what Slackware does...
	sed -i "s/#define OPENSSL_VERSION_NUMBER.*/\/* Use 0x009080efL (0.9.8o) below to avoid pointlessly breaking the ABI *\/\n#define OPENSSL_VERSION_NUMBER 0x009080efL/g" crypto/opensslv.h 
	
	# Use .so.1, not .so.1.0.0:
	zcat "${srcdir}"/openssl.soname.diff.gz | patch -p1 --backup --verbose --suffix=.orig
}

build() {
	cd "${srcdir}/openssl-${pkgver}"
	
	# Ensure ls field counts are right for parsing
	export LC_ALL=C
	
	# Disable RC5 due to patent expiring in 2015-03-03 
	./config --prefix=/usr --openssldir=/etc/ssl --libdir=lib/${MULTIARCH} \
	         no-rc5 shared
	make depend
	make
}

package() {
	cd "${srcdir}/openssl-${pkgver}"
	make install INSTALL_PREFIX="${pkgdir}"
	
	# Create symlinks
	( cd "${pkgdir}"/usr/lib/${MULTIARCH} ; ldconfig -l lib*.so.* )	
	
	# Install copyright notice
	mkdir -p "${pkgdir}"/usr/doc/openssl-${pkgver}/
	cp -a LICENSE doc "${pkgdir}"/usr/doc/openssl-${pkgver}/

	# What the heck are manpages doing in /etc?!
	mv "${pkgdir}"/etc/ssl/man "${pkgdir}"/usr/
}
